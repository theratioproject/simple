/* 
	Copyright (c) 2018 Azeez Adewale <azeezadewale98@gmail.com"> 
	MIT License Copyright (c) 2018 simple 

*/

/* 
 * @filename - Throw.sim
 * @author - Azeez Adewale
 * @date - 18 Febuary 2018
 * @Time - 02:37 PM
 */

/*
	To prevent error in operation and at all level of a program the 
	@Block throw(errorvalue) and @Block throwOnly(errorvalue) can be use 
	to show error detail and print the stack trace to locate the error 
	position. 
		The @Block throw(errorvalue) shows error detail and exit the 
	program. This is most useful to prevent erroneous program and to catch 
	error just in time.
		The @Block throwOnly(errorvalue) behaves slightly different from 
	the @Block throw(errorvalue) block as it does not force the program to
	exit after the error detail has been displayed
	
*/
 
module simple.debugging

call "Debug.sim"

	__DEBUG_FINAL_ERROR_EMPTY = "The error can never be empty: forcing exit of the program"
	__DEBUG_TYPE_THROW_ONLY = 0
	__DEBUG_TYPE_THROW = 1
 
/*
	Throw an error at any point in the program with this block
	the parameter @Bold errorvalue @End is the main error message
	display when the error is thrown. Calling this block ends the 
	program after the error has been displayed. The error is 
	thrown in the format below
	@Code
	
		Line 24 -> Unknown Error Occur
			at throw() in file Throw.sim
			at line 40 at three() in file debugging.sim
			at line 37 at two() in file debugging.sim
			at line 34 at one() in file debugging.sim
			at line 29 at main() in file debugging.sim
			at line 21 in file debugging.sim
			
	@EndCode
	
	If intention is to show an error the @Block throwOnly(errorvalue)
	should be called instead because it does not exit the program
*/
block throw(errorvalue)
	if errorvalue = "" {
		errorvalue = "Unknown Error Occur"
	}
	__throw(errorvalue)
	if isLinux() {
		@ crlf
	}
	__exit__
	
/*
	Want to make the program error tolerant then this block is 
	the right block to call instead of the @Block throw because it does
	not exit the program after the error has been thrown. It definitely 
	far from safe as the reason why it is thrown is defied as the error 
	will be carried along to another throwOnly() statement because the
	error will still be active. so to prevent a very erroneous program 
	the @Block throw(errorvalue) should be called instead
*/
block throwOnly(errorvalue)
	if errorvalue = "" {
		errorvalue = "Unknown Error Occur"
	}
	__throw(errorvalue)
	if isLinux() {
		@ crlf
	}
	
block throwWithTitle(title,errorvalue)
	__throwWithTitle(title,errorvalue,__DEBUG_TYPE_THROW)
	
block throwOnlyWithTitle(title,errorvalue)
	__throwWithTitle(title,errorvalue,__DEBUG_TYPE_THROW_ONLY)

/*

	Use a custom title for error been thrown in the program 
	the integrity of the error title and content is verified to prevent vague
	error message. This block should not be called from any point in the 
	program as it a delegate block the two block that should be called instead are
	>	throwWithTitle(title,errorvalue)
	>	throwOnlyWithTitle(title,errorvalue)
	
*/
block __throwWithTitle(title,errorvalue,type)
	finalErrorValue = ""
	if title == "" {
		finalErrorValue += ""
	else
		finalErrorValue += title
	}
	finalErrorValue += " : "
	if errorvalue == "" {
		finalErrorValue += "Unknown Error Occur"
	else
		finalErrorValue += errorvalue
	}
	if finalErrorValue == "" {
		throw(__FINAL_ERROR_EMPTY)
		__exit__
	}
	if type == __DEBUG_TYPE_THROW_ONLY {
		throwOnly(finalErrorValue)
	else
		throw(finalErrorValue)
	}



	
	